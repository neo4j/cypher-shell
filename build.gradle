buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.0'
    }
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.incremental = true
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test) {
        systemProperty 'file.encoding', 'UTF-8'
    }

    group = 'org.neo4j.shell'
    version = '1.0.0-SNAPSHOT'

    repositories {
        jcenter()
        maven {
            url "http://m2.neo4j.org/content/repositories/snapshots"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'distribution'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply from: "$rootProject.projectDir/gradle/integration-test.gradle"
    apply plugin: 'jacoco'
    apply plugin: 'info.solidsoft.pitest'

    jacocoTestReport {
        reports {
            html.enabled true
            xml.enabled true
            csv.enabled false
        }
    }

    pitest {
        threads = 4
        timestampedReports = false
        enableDefaultIncrementalAnalysis = true
        outputFormats = ['XML', 'HTML']
    }
}

ext {
    // Only match tags starting with a number to avoid neo4j-xxx changelog tags
    gitVersion = 'git describe --tags --match [0-9]*'.execute([], project.rootDir).text.trim()

    if (project.hasProperty('buildVersion')) {
        buildVersion = project.buildVersion
    } else {
        buildVersion = gitVersion
    }

    argparse4jVersion = '0.7.0'
    junitVersion = '4.12'
    evaluatorVersion = '3.5.4'
    neo4jJavaDriverVersion = '1.7.4'
    findbugsVersion = '3.0.0'
    jansiVersion = '1.13'
    jlineVersion = '2.14.6'
    mockitoVersion = '1.9.5'
    commonsIoVersion = '2.6'
}
