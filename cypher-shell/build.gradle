version = gitVersion

task generateBuildProperties {
  def outputDir = file("${buildDir}/resources/main")
  doFirst {
    outputDir.exists() || outputDir.mkdirs()
    def propFile = new File(outputDir, "build.properties")
    def props = new Properties()
    props.setProperty("version", gitVersion)
    props.store(propFile.newWriter(), null)
  }
}

processResources.dependsOn generateBuildProperties

jar {
  manifest {
    attributes 'Main-Class': 'org.neo4j.shell.Main'
  }
}

distributions {
  main {
    baseName = 'cypher-shell'
    contents {
      from {'src/dist'}
      from shadowJar
    }
  }
}

dependencies {
    compile "net.sourceforge.argparse4j:argparse4j:$argparse4jVersion"
    compile "org.neo4j.driver:neo4j-java-driver:$neo4jJavaVersion"
    compile "com.google.code.findbugs:annotations:$findbugsVersion"
    compile "org.fusesource.jansi:jansi:$jansiVersion"
    compile("jline:jline:$jlineVersion") {
        exclude(group: 'junit', module: 'junit')
    }
    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
}
